plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'com.google.gms.google-services'
}

apply plugin: 'kotlin-kapt'
apply plugin: 'kotlin-parcelize'
apply plugin: 'dagger.hilt.android.plugin'

// my config "plugin"
apply from: '../config/detekt/detekt.gradle'

// Keys
def apikeyProperties = new Properties()
def apikeyPropertiesFile = rootProject.file("apikey.properties")
apikeyProperties.load(new FileInputStream(apikeyPropertiesFile))

android {
    compileSdk 32

    signingConfigs {
        release {
            storeFile file('/home/bed/Documentos/Code/Android/Nativo/bed-app/store/app-bed-store.jks')
            keyAlias apikeyProperties['KEY_ALIAS']
            keyPassword apikeyProperties['KEY_PASSWORD']
            storePassword apikeyProperties['STORE_PASSWORD']
        }
    }

    defaultConfig {
        applicationId 'github.bed72.bedapp'
        minSdk 24
        targetSdk 32
        versionCode 1
        versionName '1.0'

        // Replace com.example.android.dagger with your class path.
        // testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        testInstrumentationRunner = "github.bed72.bedapp.CustomTestRunner"
        // The following argument makes the Android Test Orchestrator run its
        // "pm clear" command after each test invocation. This command ensures
        // that the app's state is completely cleared between tests.
        testInstrumentationRunnerArguments clearPackageData: 'true'

        // should correspond to key/value pairs inside the file
        buildConfigField 'String', 'PUBLIC_KEY', apikeyProperties['PUBLIC_KEY']
        buildConfigField 'String', 'PRIVATE_KEY', apikeyProperties['PRIVATE_KEY']
        buildConfigField 'String', 'BASE_URL', '"https://gateway.marvel.com/v1/public/"'
    }

    testOptions {
        execution 'ANDROIDX_TEST_ORCHESTRATOR'
    }

    buildTypes {
        debug {
            debuggable true
            applicationIdSuffix '.debug'
        }

        staging {
            initWith debug
            minifyEnabled true
            applicationIdSuffix '.staging'
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'),
                    'proguard-rules.pro', 'proguard-rules-staging.pro'
        }

        release {
            minifyEnabled true
            shrinkResources true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_11
        targetCompatibility JavaVersion.VERSION_11
    }

    kotlinOptions {
        jvmTarget = '11'
        freeCompilerArgs += ['-Xopt-in=kotlin.RequiresOptIn']
    }

    buildFeatures {
        viewBinding true
    }
}

dependencies {
    // My Modules
    implementation project(':core')

    // AndroidX
    implementation 'androidx.core:core-ktx:1.7.0'
    implementation 'androidx.appcompat:appcompat:1.4.1'
    implementation 'androidx.core:core-splashscreen:1.0.0-beta01'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.3'

    // Firebase
    implementation 'com.google.firebase:firebase-analytics-ktx:20.0.2'
    implementation platform('com.google.firebase:firebase-bom:29.0.3')

    // Material design
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'com.google.android.material:material:1.5.0'

    // Dagger Hilt
    kapt "com.google.dagger:hilt-android-compiler:$hilt_version"
    implementation "com.google.dagger:hilt-android:$hilt_version"

    // Other Libs
    implementation 'com.facebook.shimmer:shimmer:0.5.0'
    implementation 'androidx.datastore:datastore-preferences:1.0.0'

    // Paging3
    implementation "androidx.paging:paging-runtime-ktx:$paging_version"

    // Navigation
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"

    // Coroutines
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"

    // Glide
    def glide_version = '4.12.0'
    kapt "com.github.bumptech.glide:compiler:$glide_version"
    implementation "com.github.bumptech.glide:glide:$glide_version"

    // Room
    def room_version = '2.4.1'
    kapt "androidx.room:room-compiler:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    implementation "androidx.room:room-runtime:$room_version"

    // ViewModel and LiveData
    def lifecycle_version = '2.4.0'
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"

    // Unit tests
    testImplementation project(':testing')
    testImplementation "androidx.room:room-testing:$room_version"

    androidTestImplementation 'androidx.test:runner:1.4.0'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'com.squareup.okhttp3:mockwebserver:4.9.3'
    androidTestImplementation "com.google.dagger:hilt-android-testing:$hilt_version"

    def espresso_version = '3.4.0'
    androidTestImplementation "androidx.test.espresso:espresso-core:$espresso_version"
    androidTestImplementation "androidx.test.espresso:espresso-contrib:$espresso_version"

    androidTestUtil 'androidx.test:orchestrator:1.4.1'

    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$hilt_version"

    debugImplementation 'androidx.fragment:fragment-testing:1.4.1'
    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.8.1'
}